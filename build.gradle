buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = "1.0.0-" + (System.env.TRAVIS ? System.env.TRAVIS_BUILD_NUMBER : "local")
group = "com.github.mffbrokenswing"
archivesBaseName = "broken-library"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.2.2654"
    runDir = "run"
    mappings = "snapshot_20171003"
    makeObfSourceJar = false
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

def pomConfig = {
    name 'Broken Library'
    packaging 'jar'

    description 'A library providing development tools for Minecraft modders'
    url 'https://github.com/MFFbrokenSwing/BrokenLib'

    scm {
        connection 'scm:git:git://github.com/MFFbrokenSwing/BrokenLib.git'
        developerConnection 'scm:git:ssh://github.com:MFFbrokenSwing/BrokenLib.git'
        url 'http://github.com/MFFbrokenSwing/BrokenLib/tree/master'
    }

    licenses {
        license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    developers {
        developer {
            id 'mffbrokenswing'
            name 'Florent HUGOUVIEUX'
            email 'fhugouvieux@gmail.com'
        }
    }
}

publishing {
    publications {
        Publish(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'com.github.mffbrokenswing'
            artifactId 'broken-library'
            version project.version
            pom.withXml {
                def root = asNode()
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    publications  = ['Publish']
    pkg {
        repo = 'broken-library'
        name = 'broken-library'
        version {
            name = project.version
            desc = 'Library mod for Minecraft modders'
            released = new Date()
            vcsTag = project.version
        }
    }
}